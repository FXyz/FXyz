plugins {
    id 'application'
    id 'org.beryx.jlink' version '2.20.0'
}

sourceCompatibility = '1.9'
targetCompatibility = '1.9'
    
repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

application {
    mainModule = moduleName
}

applicationName = 'FXyz-Samples'
mainClassName = "${fxyz_sampler_mainClass}"
ext.mainClass = mainClassName

dependencies {
    implementation project(':FXyz-Client')
    implementation project(':FXyz-Core')
    implementation project(':FXyz-Importers')
    implementation ('org.controlsfx:controlsfx:11.0.1') {
        exclude group:"org.openjfx"
    }
    implementation ('org.reactfx:reactfx:2.0-M5')
    implementation ('org.fxmisc.easybind:easybind:1.0.4-SNAPSHOT')
    implementation ('org.jfxtras:jfxtras-controls:9.0-r1')
    implementation "org.graalvm.js:js:20.2.0"
    implementation "org.graalvm.js:js-scriptengine:20.2.0"
}

compileJava {
    doFirst {
        options.compilerArgs += [
                '--add-exports', 'javafx.graphics/com.sun.javafx.scene=org.fxyz3d.FXyz.Samples'
        ]
    }
    options.encoding = "UTF-8"
}

run {
    doFirst {
        jvmArgs += ['--add-exports', 'javafx.base/com.sun.javafx.runtime=org.controlsfx.controls',
                   '--add-exports', 'javafx.graphics/com.sun.javafx.scene=org.fxyz3d.FXyz.Samples']
    }
}

jar {
    manifest {
        attributes  'Implementation-Title':    'FXyz-Samples',
                    'Implementation-Version':   project.version,
                    'Class-Path':               configurations.compile.collect { it.getName() }.join(' '),
                    'Main-Class':               fxyz_sampler_mainClass,
                    'Automatic-Module-Name' :   "org.fxyz3d.samples"
    }
    from sourceSets.main.allJava
}

jlink {
    imageName = 'FXyz'
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    addExtraDependencies("javafx")
    launcher {
        name = 'FXyzSamples'
        jvmArgs = ['--add-exports', 'javafx.base/com.sun.javafx.runtime=org.fxyz3d.merged.module', 
                   '--add-exports', 'javafx.graphics/com.sun.javafx.scene=org.fxyz3d.FXyz.Samples']
    }
}