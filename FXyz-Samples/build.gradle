plugins {
    id 'application'
    id 'org.beryx.jlink' version '2.26.0'
}

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

ext.moduleName = 'org.fxyz3d.samples'

base {
    archivesName = project.name.toLowerCase()
}

application {
    mainModule = moduleName
    mainClass = "${fxyz_sampler_mainClass}"
}

dependencies {
    implementation project(':FXyz-Client')
    implementation project(':FXyz-Core')
    implementation project(':FXyz-Importers')
    implementation ('org.controlsfx:controlsfx:11.1.2') {
        exclude group:"org.openjfx"
    }
    implementation ('org.reactfx:reactfx:2.0-M5')
    implementation ('org.fxmisc.easybind:easybind:1.0.4-SNAPSHOT')
    implementation ('org.jfxtras:jfxtras-controls:17-r1')
    implementation "org.graalvm.js:js:22.3.0"
    implementation "org.graalvm.js:js-scriptengine:22.3.0"
}

extraJavaModuleInfo {
    failOnMissingModuleInfo.set(true)
    automaticModule('eu.mihosoft.vrl.jcsg:jcsg', 'jcsg')
    automaticModule('eu.mihosoft.vvecmath:vvecmath', 'vvecmath')
    automaticModule('org.orbisgis:poly2tri-core', 'poly2tri.core')
    automaticModule('org.reactfx:reactfx', 'reactfx')
    automaticModule('org.fxmisc.easybind:easybind', 'easybind')
}

compileJava {
    doFirst {
        options.compilerArgs += [
                '--add-exports', 'javafx.graphics/com.sun.javafx.scene=org.fxyz3d.samples'
        ]
    }
    options.encoding = "UTF-8"
}

run {
    doFirst {
        jvmArgs += ['--add-exports', 'javafx.base/com.sun.javafx.runtime=org.controlsfx.controls',
                    '--add-exports', 'javafx.graphics/com.sun.javafx.scene=org.fxyz3d.samples']
    }
}

jar {
    manifest {
        attributes  'Implementation-Title':    'FXyz-Samples',
                    'Implementation-Version':   project.version,
                    'Class-Path':               configurations.runtimeClasspath.collect { it.getName() }.join(' '),
                    'Main-Class':               fxyz_sampler_mainClass,
                    'Automatic-Module-Name' :   "org.fxyz3d.samples"
    }
    from sourceSets.main.allJava
}

jlink {
    imageName = 'FXyz'
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    addExtraDependencies("javafx")
    launcher {
        name = 'FXyzSamples'
        jvmArgs = ['--add-exports', 'javafx.base/com.sun.javafx.runtime=org.fxyz3d.merged.module',
                   '--add-exports', 'javafx.graphics/com.sun.javafx.scene=org.fxyz3d.FXyz.Samples']
    }
}
