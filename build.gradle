import java.text.SimpleDateFormat

plugins {
    id 'org.openjfx.javafxplugin' version '0.0.7'
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'net.nemerosa.versioning' version '2.7.1'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.github.ben-manes.versions' version '0.13.0'
    id "com.github.johnrengelman.shadow" version "4.0.3"
}

apply plugin: 'base'

Properties cfg = new Properties()
cfg.load(new FileInputStream("$rootDir/fxyz-build.properties"))

ext {
    artifact_suffix          = cfg.artifact_suffix
    specification_title      = cfg.fxyz_specification_title
    specification_version    = cfg.fxyz_specification_version
    fxyz_name                = 'FXyzLibs'
    fxyz_sampler_name        = 'FXyz-Sampler'
    fxyz_sampler_version     = cfg.fxyz_sampler_specification_version + artifact_suffix
    fxyz_sampler_mainClass   = 'org.fxyz3d.samples.Launcher'
    fxyz_group               = cfg.fxyz_group
}
    
subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'eclipse'
    apply plugin: 'org.openjfx.javafxplugin'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'com.github.hierynomus.license'

    group = fxyz_group
    version = specification_version + artifact_suffix

    repositories {
        jcenter()
        maven {
            url = "http://maven.jzy3d.org/releases"
        }
    }
    
    dependencies {     
        implementation ('eu.mihosoft.vrl.jcsg:jcsg:0.5.7') { transitive = false }
        implementation 'eu.mihosoft.vvecmath:vvecmath:0.3.8'
        implementation 'org.orbisgis:poly2tri-core:0.1.2'
        // A fork of JDT (Java Delaunay Triangulation) Core API from https://github.com/yonatang/JDT for Jzy3d
        implementation 'org.jzy3d:jzy3d-jdt-core:1.0.2'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.1'
        testImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
    }
    
    javafx {
        modules = [ 'javafx.controls', 'javafx.fxml', 'javafx.swing' ]
    }

    eclipse {
        classpath {
            file {
                whenMerged {
                    entries.findAll { it.kind == 'src' || it.kind == 'lib' }.each {
                        it.entryAttributes['module'] = 'true'
                    }
                }
            }
        }
    }
	
    task wrapper(type: Wrapper) {
        gradleVersion = '5.0'
    }

    task sourceJar(type: Jar) {
        from sourceSets.main.java
        from sourceSets.main.resources
        classifier = 'sources'
    }

    artifacts {
        archives sourceJar
    }

    license {
        header = rootProject.file('fxyz-license.txt')
        strictCheck = true
        ignoreFailures = false
        skipExistingHeaders = true
        mapping {
            java   = 'SLASHSTAR_STYLE'
            groovy = 'SLASHSTAR_STYLE'
            fxml   = 'XML_STYLE'
        }

        ext.title           = 'F(X)yz'
        ext.yearCurrent     = new SimpleDateFormat("yyyy").format(new Date())
        ext.yearSince       = '2013'

        excludes(['**/*.txt', '**/*.html', '**/*.fxml', '**/*.css', '**/*.obj', '**/*.mtl', '**/*.ma', '**/*.png', '**/*.gif', '**/*.jpg'])
    }

    test {
        useJUnitPlatform()

        testLogging {
            events "failed"
            exceptionFormat "full"
        }
    }
}